Actor Pumping : Inventory
{
Inventory.MaxAmount 1
}

Actor HasSlugs : Inventory
{
Inventory.MaxAmount 1
}

Actor HasDragonBreath : Inventory
{
Inventory.MaxAmount 1
}

Actor DragonBreathUpgrade : Inventory
{
Inventory.MaxAmount 1
}

ACTOR IsCocking : Inventory
{
Inventory.MaxAmount 1
}

Actor ShotgunWasEmpty : Inventory
{
inventory.maxamount 1
}

Actor PBPumpShotgunWasEmpty : Inventory
{
inventory.maxamount 1
}

Actor PBPumpShotgunHasUnloaded: Inventory
{
Inventory.maxamount 1
}

ACTOR RespectShotgun : Inventory
{
Inventory.MaxAmount 2
}

Actor PumpshotgunMagazine: Inventory
{
inventory.maxamount 1
}

Actor PumpshotgunMagNotInserted: Inventory
{
inventory.maxamount 1
}

Actor PumpshotgunUpgrade: Custominventory
{
//$Category Ammunition
	SpawnID 9310
	Game Doom
	Height 24
	//-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+COUNTITEM
	Inventory.Pickupsound "SHOTPICK"
	Inventory.PickupMessage "You got the Pump shotgun Magazine Upgrade!!!"
	States
	{
	Spawn:
		9SMU ABCDEDCB 2 BRIGHT
		Loop
	
    Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("PumpShotgunMag", 0)
		TNT1 A 0
		{
		A_GiveInventory("Shot_Gun", 1);
		A_GiveInventory("DragonBreathUpgrade", 1);
		A_GiveInventory("NewShell", 20);
		A_GiveInventory("ShotgunAmmo", 11);
		A_GiveInventory("PumpshotgunMagazine", 1);
		A_GiveInventory("PumpshotgunMagNotInserted", 1);
		}
		Stop
	}
}

Actor DragonBreathShellsUpgrade: Custominventory
{
//$Category Ammunition
	Game Doom
	SpawnID 9320
	Height 24
	//-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	+COUNTITEM
	Inventory.Pickupsound "misc/shellbox_PickUp"
	Inventory.PickupMessage "You got the Dragon's Breath Shell Upgrade!!!"
	States
	{
	Spawn:
		DHEL ABCDEDCB 2 BRIGHT Light("WeaponUpgradeSpawner")
		Loop
	
    Pickup:
	  TNT1 AA 0
	  {
	   A_GiveInventory("DragonBreathUpgrade", 1);
	   A_GiveInventory("NewShell", 20);
	  }
      Stop
	}
}

Actor DragonBreath
{
	Projectile
	+RANDOMIZE
+FORCEXYBILLBOARD
+RIPPER
+NOEXTREMEDEATH
damage 5
radius 1
height 1
speed 40
renderstyle ADD
alpha 0.9
scale .15
    DamageType Fire
	SeeSound "Afrit/Hellfire"
    Decal "SmallerScorch"
states
	{
	Spawn:
	TNT1 A 0 A_Playsound("Afrit/Hellfire")
	TNT1 A 0 A_Explode(21, 66, 0)
	TNT1 AAAA 0 A_SpawnItemEx ("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	EXPL A 0 A_SpawnItemEx ("ExplosionParticleSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	EXPL AAAAAAAAAAA 0 A_CustomMissile ("ShotgunParticles", 6, 0, random (0, 360), 2, random (0, 90))//ShotgunParticles
	TNT1 A 0 A_Jump(76, 2, 3)
	TNT1 A 0 A_SpawnItemEx("DragonsBreathPiece1", random (-15, 15), random (-15, 15))
	TNT1 A 0 A_SpawnItemEx("DragonsBreathPiece2", random (-35, 35), random (-35, 35))
	TNT1 A 0 A_SpawnItemEx("DragonsBreathPiece3", random (-45, 45), random (-45, 35))
	TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile ("SparkX", 2, 0, random (0, 360), 2, random (0, 360))
    TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
	Stop
Death:
    Stop
XDeath:
TNT1 A 0
Stop
	}
}

ACTOR DragonsBreathTracer: FastProjectile
{
	Projectile
	+RANDOMIZE
+FORCEXYBILLBOARD
+DONTSPLASH
+NOEXTREMEDEATH
Damagetype Shotgun
Damage 7
PoisonDamageType Fire
PoisonDamage 7
+ADDITIVEPOISONDURATION
radius 2
height 2
speed 80
Gravity 0.4
renderstyle ADD
alpha 0.95
scale 0.45
    Decal "SmallerScorch"
states
	{
	Spawn:
		DBAC A 3 BRIGHT
		TNT1 A 0 A_SpawnItemEx("ShotgunParticles", random(8,-8), random(8,-8), random(8,-8), 0, 0, 0, 0, 128, 0)
		TNT1 A 0 A_SpawnItemEx("ExplosionParticleVerySlow", random(8,-8), random(8,-8), random(19,-19), 0, 0, 0, 0, 128, 0)
		Loop
Death:
XDeath:
	TNT1 A 0 
	{
	A_Explode(5, 30, 0);
	A_Playsound("ricochet/hit");
    A_SpawnItemEx ("DragonBreath",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION,0);
	}
    Stop
	}
}

/*---------------------------------------------------------------------------

   12 Gauge Pump Shotgun (With and Without The Magazine Upgrade)

-----------------------------------------------------------------------------*/

// If you're looking specifically for the mag upgrade coding, look in the pump and reloading states, weapon special states
// kickflash states, and unload states.
// On a side note, this weapon has got complex as hell.

ACTOR Shot_Gun : PB_Weapon Replaces Shotgun
{
	SpawnID 9300
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	+FLOORCLIP
	+DONTGIB
	Game Doom
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 4
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "NewShell"
	Weapon.AmmoType2 "ShotgunAmmo"
	Inventory.PickupMessage "You got the Pump Shotgun! (Slot 3)"
    Inventory.PickupSound "SHOTPICK"
	Obituary "%o sucked %k's Shotgun"
    AttackSound "None"
	Tag "Shotgun"
    +WEAPON.NOALERT
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	PB_WeaponBase.UnloaderToken "PBPumpShotgunHasUnloaded"
	Scale 1.15
	States
	{
	
	NoAmmo:
		TNT1 A 0
		{
		A_PlaySound("weapons/empty");
		A_TakeInventory("Zoomed", 1);
		A_ZoomFactor(1.0);
		A_TakeInventory("ADSmode", 1);
		A_TakeInventory("Reloading", 1);
		}
		Goto Ready3
		
	Ready:
		TNT1 AA 0 A_JumpIfInventory("GoFatality", 1, "Steady")
	ShotgunRespect:
	    TNT1 A 0 A_GiveInventory("ShotgunAmmo",9)
		TNT1 A 0 A_JumpIfInventory("RespectShotgun", 1, "SelectAnimation")
		SHTS ABCDJKLMNOP 0 // Virtualize Sprites in gzdoom's memory
		SHTD ABCDJKLMNOP 0
		SHMS BCDEFGSTUVWX 0
		SHMD BCDEFGSTUVWX 0
		SHZA DEFGHIJKLMNO 0
	    SHZB DEFGHIJKLMNO 0
		SHT4 DFGHIJKLMN 0
		SHT6 DFGHIJKLMN 0
		TNT1 A 0 A_GiveInventory("RespectShotgun",1)
	    SHTZ ABCDEFGHIJKLMNOP 1 A_WeaponReady(WRF_NOBOB)
		SHTG ABCDEFGHI 1 A_WeaponReady(WRF_NOBOB)
		SSHR P 2 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_PlaySound("weapons/sgmvpump",1)
		SSHR I 1 A_WeaponReady(WRF_NOBOB)
		SSHR J 1
		{
		A_WeaponReady(WRF_NOBOB);
		A_PlaySound("insertshell"); 
		}
		SSHR KLMNOPP 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_GiveInventory("RespectShotgun",1) 
		TNT1 A 0 A_JumpIfInventory("PumpshotgunMagNotInserted", 1, "InsertMagShotgunRespect")
		TNT1 A 0 A_PlaySound("weapons/sgmvpump",1)
		SHTG IHGFEDCB 1 A_WeaponReady(WRF_NOBOB)
	FinishInspect:
	    SHTG A 1 A_WeaponReady(WRF_NOBOB)
		SHT8 EDK 1 A_WeaponReady(WRF_NOBOB)
		SHT8 A 10 A_WeaponReady(WRF_NOBOB)
		SHT8 KDE 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready3
	InsertMagShotgunRespectAlreadyRespected:
	    TNT1 A 0
		TNT1 A 0 A_TakeInventory("RespectShotgun",1) 
	InsertMagShotgunRespect:
	    TNT1 A 0 A_TakeInventory("PumpshotgunMagNotInserted",1)
		TNT1 A 0 ACS_NamedExecuteAlways("PumpShotgunMagDualTube", 0)
		SHTG A 1 A_WeaponReady(WRF_NOBOB)// The script needs 1 tick to process itself.
		TNT1 A 0 A_GiveInventory("ShotgunAmmo",19)
		TNT1 A 0 A_JumpIfInventory("RespectShotgun", 1,"InsertMagBegin")
		SHTG BCDEFGHIJ 1 A_WeaponReady(WRF_NOBOB)
	InsertMagBegin:	
		SHTM A 1 A_WeaponReady(WRF_NOBOB)
		SHTM BCDEFG 1 
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHMS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHMD");}
		 A_WeaponReady(WRF_NOBOB);
		 return state("");
		}
		SHTM H 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_PlaySound("RIFCL_IN", 3)
		SHTM I 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_PlaySound("insertshell", 4)
        SHTM JKLM 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_PlaySound("RIFCL_CL", 5)
		SHMG IHG 1 A_WeaponReady(WRF_NOBOB)
		SHTG FEDCB 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_JumpIfInventory("RespectShotgun", 2, "FinishInspect")
		TNT1 A 0 A_GiveInventory("RespectShotgun",1) 
		Goto Ready3
	SelectAnimation:	
		SHSS ABCD 1
        TNT1 A 0
		{
		  A_PlaySound("weapons/sgmvpump");
	      A_Giveinventory("PB_LockScreenTilt",1); 
		  A_SetRoll(0,SPF_INTERPOLATE); // Resets the camera back to default.
		  A_Takeinventory("PB_LockScreenTilt",1); 
		}
	Ready3:
		SHTG A 1
		{
	     if (CountInv("HasSlugs") >= 1 )                  { A_SetWeaponSprite("SHTS"); }
		 if (CountInv("HasDragonBreath") >= 1 )           { A_SetWeaponSprite("SHTD"); }
		 if (CountInv("PumpshotgunMagNotInserted") >= 1 ) { return state("InsertMagShotgunRespectAlreadyRespected"); } // Insert magazine upon picking it up
		 return A_DoPBWeaponAction(WRF_ALLOWRELOAD, CheckUnloaded("PBPumpShotgunHasUnloaded"));
		}
		Loop
			 
	Ready2:
		SHT8 A 1
		{		
		 if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHT6"); }
		 if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHT4"); }
		 if (CountInv("PumpshotgunMagNotInserted") >= 1 ) { return state("InsertMagShotgunRespectAlreadyRespected"); }
		 return A_DoPBWeaponAction(WRF_ALLOWRELOAD, CheckUnloaded("PBPumpShotgunHasUnloaded"));
	    }
		Loop
				
	WeaponSpecial:
		TNT1 A 0
		TNT1 A 0 A_Takeinventory("GoWeaponSpecialAbility",3)
		TNT1 A 0
		{
	     if (CountInv("HasSlugs") >= 1 )        {return state("SwitchToDragonBreath");}
	     if (CountInv("HasDragonBreath") >= 1 ) {return state("SwitchToBuckshot");}
		 return state("");
	    }
		TNT1 A 0
		{
		 A_Print("Slug Shells Loaded");
	     A_Takeinventory("GoWeaponSpecialAbility",3);
         A_GiveInventory("HasSlugs",1);
		}
		Goto AlternateWeaponSpecialAnimationSwitch
		
	SwitchToDragonBreath:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("DragonBreathUpgrade", 1, 1)
		Goto SwitchToBuckshot
		TNT1 A 0
		{ 
		 A_Print("Dragon's Breath Shells Loaded");
         A_Takeinventory("GoWeaponSpecialAbility",3); 
         A_Takeinventory("HasSlugs", 1); 
         A_GiveInventory("HasDragonBreath",1);
		}
		Goto AlternateWeaponSpecialAnimationSwitch
		
	SwitchToBuckshot:
		TNT1 A 0
		TNT1 A 0
		{ 
         A_Print("Buckshot Shells Loaded");
         A_Takeinventory("GoWeaponSpecialAbility",1);
         A_Takeinventory("HasSlugs", 1);
         A_Takeinventory("HasDragonBreath",1);
		}
		Goto AlternateWeaponSpecialAnimationSwitch
	     
  AlternateWeaponSpecialAnimationSwitch:
	TNT1 A 0
	SHTZ PON 1 
	    {
         if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHZA"); }
         if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHZB"); }
	    }
	SHTZ M 1
	SHTZ LKJIHGFED 1
	    {
         if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHZA"); }
		 if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHZB"); }
	    }
	SHTZ CBA 1
	SHTZ ABC 1 
	SHTZ DEFGHIJKL 1
	    {
         if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHZA"); }
         if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHZB"); }
	    }
	SHTZ M 1
	SHTZ NOP 1
	    {
         if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHZA"); }
         if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHZB"); }
	    }
 FullyAttached:
    TNT1 A 0 
	SHTG A 1  
	    {	
	     if (CountInv("HasSlugs") >= 1 )        { A_SetWeaponSprite("SHTS"); }
	     if (CountInv("HasDragonBreath") >= 1 ) { A_SetWeaponSprite("SHTD"); }
	    }
	Goto Ready3
	
	Deselect:
		TNT1 A 0
	    {
		 A_Takeinventory("Unloading",1);
		 A_TakeInventory("Zoomed",1);
         A_ZoomFactor(1.0);
		 A_TakeInventory("ADSmode",1);
		}
        SHSS DCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
		
	Select:
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 ACS_NamedExecuteAlways("ToggleShotgunUpgraded",0) //For people who want this weapon to replace another
		TNT1 A 0 
		TNT1 A 0 
		{
		 A_Takeinventory("Unloading",1);
		 A_TakeInventory("RifleSelected",1);
		 A_TakeInventory("CarbineSelected",1);
		 A_TakeInventory("FistsSelected",1);
		 A_TakeInventory("SawSelected",1);
		 A_TakeInventory("SledgeSelected",1);
		 A_TakeInventory("HandgunSelected",1);
		 A_TakeInventory("DualHandgunSelected",1);
		 A_TakeInventory("RevolverSelected",1);
		 A_GiveInventory("ShotgunSelected",1);
		 A_TakeInventory("SSGSelected",1);
		 A_TakeInventory("ASGSelected",1);
		 A_TakeInventory("QSGSelected",1);
		 A_TakeInventory("MinigunSelected",1);
		 A_TakeInventory("NewChaingunSelected",1);	
	     A_TakeInventory("RocketLauncherSelected",1);
		 A_TakeInventory("GrenadeLauncherSelected",1);
		 A_TakeInventory("SGLSelected",1);
		 A_TakeInventory("PlasmaGunSelected",1);
		 A_TakeInventory("M2Selected",1);	
		 A_TakeInventory("FreezerSelected",1);
		 A_TakeInventory("RailGunSelected",1);
		 A_TakeInventory("BFGSelected",1);
		 A_TakeInventory("BFGBeamSelected",1);
		 A_TakeInventory("BHCSelected",1);
		 A_TakeInventory("RevenantLauncherSelected",1);
	     A_TakeInventory("FlameCannonSelected",1);
		 A_TakeInventory("HellRifleSelected",1);
		 A_TakeInventory("LandMineSelected",1);
		 A_TakeInventory("UACSMGSelected",1);
		 A_TakeInventory("SubMachineGunSelected",1);
		 A_TakeInventory("LostSoulSelected",1);
		 A_TakeInventory("HasBarrel",1);
		 A_TakeInventory("HasBurningBarrel",1);
		}
		TNT1 A 0 A_Raise
		Wait
	
    Fire:
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 
		{
		 if (CountInv("Zoomed") >= 1 )     {return state("Fire2");}
		 If (CountInv("ShotgunAmmo") >=1 ) {return state(1);} 
		 return state("Reload");
        }
		TNT1 A 0 
		{
		 A_TakeInventory("ShotgunAmmo", 1);
		 A_AlertMonsters;
		 A_FireCustomMissile("YellowFlareSpawn", 0, 0, 0, 0);
		 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0);
		}
		TNT1 AAAA 0 A_FireCustomMissile("ShotgunParticles", random(-17,17), 0, -1, random(-17,17))
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0)
		SHTF AB 1 BRIGHT
		TNT1 A 0 Radius_Quake(1,11,0,1,0)
		TNT1 A 0 
		{
         If (CountInv("HasSlugs") >=1 )        
		    {A_PlaySound("SlugShot", 5);
              A_FireBullets(1.5, 0.5, 1, 160, "ShotgunPuff",FBF_NORANDOM);
			   A_FireCustomMissile("Tracer", 0, 0, 0, -14, 0, 0);
			    return state("FireAnimBegin");}
		 If (CountInv("HasDragonBreath") >=1 ) 
		    {A_PlaySound("DRBTFIRE", 5);
		      A_FireBullets(2, 2,10,0,"None",FBF_NORANDOM,8192,"DragonsBreathTracer",-14,0);
		       return state("FireAnimBegin");}
		  A_PlaySound("weapons/sg", 5);   
		  A_FireBullets(2, 2,10,16,"ShotgunPuff",FBF_NORANDOM);
		   return state("");
		}
		TNT1 AAAAAA 0 A_FireCustomMissile("Tracer", random(-2,2), 0, 0, -14, 0, random(-1,1))
		
	FireAnimBegin:
		SHTF C 1 A_SetPitch(pitch-0.2)
		SHTF D 1 A_SetPitch(pitch-0.2)
		SHTF E 1 A_SetPitch(pitch-0.2)
		SHTF F 1 A_SetPitch(pitch-0.2)
		SHTF G 1 A_SetPitch(pitch-0.2)
		SHTF F 1 A_SetPitch(pitch+0.2)
		SHTF E 1 A_SetPitch(pitch+0.2)
		SHTF C 1 A_SetPitch(pitch+0.2)
		SHTG A 1 A_SetPitch(pitch+0.2)
		Goto Pump1
		
	Pump1:
	    TNT1 A 0 A_GiveInventory("IsCocking", 1)
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine", 1, "MagPump")
		SHTG CDEFGHIJK 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		SHTG K 3
	P1Begin:
	    SHTA L 0
		SHTF L 0
		SHTG L 1 
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTA");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHTF");}
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1); 
		 return state("");
		}
	P1B:
		TNT1 A 0
		{
		 If(CountInv("HasSlugs") >=1 ) {A_FireCustomMissile("ShotCaseSpawn2",0,0,-5,-21);
		 return state("P1C");}        
		 If(CountInv("HasDragonBreath") >=1 ) {A_FireCustomMissile("ShotCaseSpawn3",0,0,-5,-21);
		 return state("P1C");}
		 A_FireCustomMissile("ShotCaseSpawn",0,0,-5,-21);
		 return state("");
		}
	P1C:
		SHTG M 1
		{
		 A_SetRoll(roll-0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch+0.1,SPF_INTERPOLATE);
		}
		SHTG N 2
		SHTG M 1 
		{
		 A_SetRoll(roll+0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch-0.1,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_JumpIfInventory("NewShell", 1,2) //Skip this frame if the chamber isn't empty
		SSHR H 1 
		{
		 A_SetRoll(roll+0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1);
		 return state (2); // Skip the frame with shell showing
		}
		SHTG L 1 
		{
		 If( CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTA");} 
		 If (CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHTF");}
		 A_SetRoll(roll+0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1);
		 return state("");
		}
		SHTG KJIHGFEDCB 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
	P1End: // Pump End for mag & regular.
	    TNT1 A 0
		{
		 A_SetRoll(0,SPF_INTERPOLATE);
		 A_Takeinventory("PB_LockScreenTilt",1);
		}
        SHTG A 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHTS");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHTD");}
		 A_TakeInventory("IsCocking", 1);
		 A_WeaponReady(WRF_ALLOWRELOAD);
		 A_Refire;
		 return state("");
		}
		Goto Ready3

////Magazine Pumping.	
	MagPump: 
	    SHTG CDEF 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		SHMG GHIJK 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		SHMG K 3
	    SHMA L 0
		SHMF L 0
		SHMG L 1 
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHMA");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHMF");}
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1); 
		 return state("");
		}
	MagP1B:	
		TNT1 A 0
		{
		 If(CountInv("HasSlugs") >=1 ) {A_FireCustomMissile("ShotCaseSpawn2",0,0,-2,-18);
		 return state("MagP1C");}        
		 If(CountInv("HasDragonBreath") >=1 ) {A_FireCustomMissile("ShotCaseSpawn3",0,0,-2,-18);
		 return state("MagP1C");}
		 A_FireCustomMissile("ShotCaseSpawn",0,0,-2,-18);
		 return state("");
		}
	MagP1C:	
		SHMG M 1
		{
		 A_SetRoll(roll-0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch+0.1,SPF_INTERPOLATE);
		}
		SHTG N 2
		SHMG M 1 
		{
		 A_SetRoll(roll+0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch-0.1,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_JumpIfInventory("NewShell", 1,2) //Skip this frame if the chamber isn't empty
		SHMG N 1 
		{
		 A_SetRoll(roll+0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1);
		 return state (2);
		}
		SHMG L 1 
		{
		 If( CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHMA");} 
		 If (CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHMF");}
		 A_SetRoll(roll+0.1,SPF_INTERPOLATE);
		 A_PlaySound("weapons/sgmvpump",1);
		 return state("");
		}	
		SHMG KJIHG 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		SHTG FEDCB 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		Goto P1End
	
	Fire2:
        TNT1 A 0 A_TakeInventory("Reloading", 1)
        TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,1)  
        Goto Reload
        TNT1 A 0 
		{
		 A_AlertMonsters;
		 A_FireCustomMissile("YellowFlareSpawn", 0, 0, 0, 0);
		 A_TakeInventory("ShotgunAmmo", 1);
		}
		TNT1 AAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-17,17), 0, -1, random(-17,17))
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 2, 0, 0)
		TNT1 A 0 
		{
		 If (CountInv("PowerStrength") >=1 )   {return state(1);} 
         If (CountInv("HasSlugs") >=1 )        {A_PlaySound("SlugShot", 5); A_FireBullets(1.25, 0.25, 1, 162, "ShotgunPuff",FBF_NORANDOM,8192,"Tracer",-2,0);return state(2);}  
	     If (CountInv("HasDragonBreath") >=1 ) {A_PlaySound("DRBTFIRE", 5);A_FireBullets(3.25, 1.25,9,0,"None",FBF_NORANDOM,8192,"DragonsBreathTracer",-2,0);return state(2);}
		 else {A_PlaySound("weapons/sg", 5);    A_FireBullets(3.25, 1.25,9,18,"ShotgunPuff",FBF_NORANDOM,8192,"Tracer",-2,0);return state(2);}
		}
		TNT1 A 0
		{
         If (CountInv("HasSlugs") >=1 )        {A_PlaySound("SlugShot", 5); A_FireBullets(1.25, 0.25, 1, 162, "ShotgunPuff",FBF_NORANDOM,8192,"Tracer",-2,0);return state(1);}  
	     If (CountInv("HasDragonBreath") >=1 ) {A_PlaySound("DRBTFIRE", 5);A_FireBullets(3, 1,9,0,"None",FBF_NORANDOM,8192,"DragonsBreathTracer",-2,0);return state(1);}
		 else {A_PlaySound("weapons/sg", 5);    A_FireBullets(3, 1,9,18,"ShotgunPuff",FBF_NORANDOM,8192,"Tracer",-2,0);return state(1);}
		}
		SHT8 F 2 BRIGHT
		{
		 If (CountInv("HasSlugs") >=1 )        {A_SetWeaponSprite("SHT6");}
		 If (CountInv("HasDragonBreath") >=1 ) {A_SetWeaponSprite("SHT4");}
		 else {A_SetPitch(pitch-0.2);}
		 return state("");
		}
		TNT1 A 0 
		{
		 If (CountInv("PowerStrength") >=1 )        {A_SetPitch(pitch-0.25);return state("BerserkAltPump1");}
		 return state("");
	    }
		SHT8 GHI 1
		{
		 If (CountInv("HasSlugs") >=1 )        {A_SetWeaponSprite("SHT6");A_SetPitch(pitch-1);}
		 If (CountInv("HasDragonBreath") >=1 ) {A_SetWeaponSprite("SHT4");A_SetPitch(pitch-1);}
		 else {A_SetPitch(pitch-1);}
		 return state("");
		}
		Goto AltPump1
	
	BerserkAltPump1:
	    TNT1 A 0 A_PlaySound("weapons/sgmvpump")
	    SHT8 KLMN 1
	    {
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 return state("");
		}
	Goto AltPump1B
							
	AltPump1:
	    TNT1 A 0 A_PlaySound("weapons/sgmvpump")
		SHT8 IHGJKLMN 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 return state("");
		}
	AltPump1B:	 
		SHT8 OPQR 1 
		TNT1 A 0
		{
		 If(CountInv("HasSlugs") >=1 ) {A_FireCustomMissile("ShotCaseSpawn2",0,0,4,-7);
		 return state("AFP1E");}        
		 If(CountInv("HasDragonBreath") >=1 ) {A_FireCustomMissile("ShotCaseSpawn3",0,0,4,-7);
		 return state("AFP1E");}
		 A_FireCustomMissile("ShotCaseSpawn",0,0,4,-7);
		 return state("");
		}
	AFP1E:	 
		TNT1 A 0 A_PlaySound("weapons/sgpump")
		SHT8 STQO 1
		SHT8 NMLKJ 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 return state("");
		}
		SHT8 A 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 A_WeaponReady(WRF_ALLOWRELOAD);
		 A_ReFire;
		 return state("");
		}
		 Goto Ready2
							
	AltFire:
		TNT1 A 0
		{
		 A_WeaponOffset(0,32);
		 A_PlaySound("IronSights", 0);
		 If(CountInv("Zoomed") >=1 )  {return state("zoomout");}
		 A_GiveInventory("Zoomed",1);
         A_ZoomFactor(1.2);
		 A_SetCrosshair(5);
		 return state("");
		}
        SHT8 EEDK 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 return state("");
		}
        Goto Ready2
	zoomout:	
        TNT1 A 0 
		SHT8 KDEE 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHT6");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		 {A_SetWeaponSprite("SHT4");} 
		 return state("");
		}
		TNT1 A 0 
		{
		 A_TakeInventory("Zoomed",1);
         A_ZoomFactor(1.0);
		 A_SetCrosshair(0);
		}
        Goto Ready3
					
	ReloadWithNoAmmoLeft:
    Reload:
	    TNT1 A 0
		{
		 A_TakeInventory("Reloading", 1);
		 A_TakeInventory("Zoomed", 1);
         A_ZoomFactor(1.0);
		}
		SHTG A 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHTS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHTD");}
		 A_SetCrosshair(0);
		 A_WeaponReady;
		 return state("");
		}
		TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine",1,"MagReload")
	ReloadActuallyBegin:
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"Ready3") 
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo", 1, "ReloadNormally")
		TNT1 A 0 A_GiveInventory("PBPumpShotgunWasEmpty", 1)		
	ReloadNormally:	
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
        Goto NoAmmo
        TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1) 
		SHTG BCDEFGHIJ 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)	
    ShellChecker:
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
        Goto ReloadFinished
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"ReloadFinished")
		SSHR A 1
		{
		 A_WeaponReady(WRF_NOBOB);
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_JumpIfInventory("PBPumpShotgunWasEmpty", 1, "ChamberInsertShell")
		SSHR BCD 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHTS");}
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHTD");}
		 return state("");
		}	
		TNT1 A 0 A_PlaySound("insertshell")
		SSHR E 1 A_SetPitch(pitch-0.2,SPF_INTERPOLATE)
		SSHR FG 1
		{
		 A_SetPitch(pitch+0.2,SPF_INTERPOLATE);
		 A_WeaponReady(WRF_NOBOB);
		}
		TNT1 A 0 
		{
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		SSHR A 0 A_Refire
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Goto ShellChecker
			
	ChamberInsertShell:
        TNT1 A 0
		TNT1 A 0 A_Takeinventory("PBPumpShotgunWasEmpty",1)
		SSHR A 1 A_WeaponReady(WRF_NOBOB)
		SSHR H 2 
		TNT1 A 0 A_PlaySound("weapons/sgpump",1)
		SSHR I 3
		SSHR J 1 A_PlaySound("insertshell",3)
		SSHR KLMNOPP 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTS");}
		 If(CountInv("HasDragonBreath") >=1 )
		   {A_SetWeaponSprite("SHTD");}
		 return state("");
		}
		TNT1 A 0 
		{
		 A_PlaySound("weapons/sgmvpump",2);
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		Goto ShellChecker
	
	ReloadFinished:
	    SSHR A 1
		SHTG JIHGFEDCBA 1
		{
		 A_WeaponReady(WRF_NOBOB);
		 A_SetRoll(roll+0.1,SPF_INTERPOLATE);
		}	
		TNT1 A 0
		{
		 A_Takeinventory("Reloading",1);
		 A_Takeinventory("PB_LockScreenTilt",1);
		 A_SetRoll(0,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_Refire
		Goto Ready3
	
////Mag Reload	
	MagReload:
		TNT1 A 0 A_jumpif(GetCvar("pb_SGDualTubeSystem") >= 1,"DualTubeReload") // Seperate Reload State for this.
	BeginMagReload:
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",11,"Ready3")
		TNT1 A 0 ACS_NamedExecuteAlways("PumpShotgunMagDualTube11", 0)
		SHTG BCDEF 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		SHMG GHI 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
	ActuallyBeginMagReload:	
	    TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",11,"Ready3")
		SHTM MQR 1
		TNT1 A 0
		{
		if (CountInv("ShotgunAmmo") <= 0) {return state("EM");}
		return state("");
		}
		SHTM STUVWX 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHMS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHMD");}
		}
		Goto InsertMag
	EM:
	    SHTN ABCD 1
	InsertMag:
	    SHTM A 1
	    SHTM BCDEFG 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHMS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHMD");}
		}
		SHTM H 1 
		TNT1 A 0 A_PlaySound("RIFCL_IN", 3)
		SHTM I 1 
		TNT1 A 0 A_PlaySound("insertshell", 4)
        SHTM JKLM 1 
	ShellCheckerMag:	
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
        Goto ReloadMagFinished
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",11,"ReloadMagFinished")
		TNT1 A 0 
		{
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		Goto ShellCheckerMag
	ReloadMagFinished:
	    SHMG IHG 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		SHTG FEDCB 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		TNT1 A 0
		{
		 A_Takeinventory("Reloading",1);
		 A_Takeinventory("PB_LockScreenTilt",1);
		 A_SetRoll(0,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_Refire
		Goto Ready3
		
////Dual Tube Reload	
	DualTubeReload: // Duplicate sections of code, but it's too annoying doing this the other way, and it's easier to remove if it's ever needed.
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",19,"Ready3")
		TNT1 A 0 ACS_NamedExecuteAlways("PumpShotgunMagDualTube19", 0)
		SHTG BCDEF 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		SHMG GHI 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
	    TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",19,"Ready3")
		SHTM MQRRR 1
		TNT1 A 0
		{
		if (CountInv("ShotgunAmmo") <= 0) {return state("DTEM");}
		return state("");
		}
		SHTM STUVWX 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHMS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHMD");}
		 return state("");
		}
		Goto DTShellChecker
	DTEM:
	    SHTN ABCD 1
	DTShellChecker:
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
        Goto DTReloadFinished
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",9,"DTReloadFinished")
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo", 1, "DTReloadNormally")
		TNT1 A 0 A_GiveInventory("PBPumpShotgunWasEmpty", 1)
	DTReloadNormally:	
		SSHR A 1
		{
		 A_WeaponReady(WRF_NOBOB);
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_JumpIfInventory("PBPumpShotgunWasEmpty", 1, "DTChamberInsertShell")
		SSHR BCD 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHTS");}
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHTD");}
		 return state("");
		}	
		TNT1 A 0 A_PlaySound("insertshell")
		SSHR E 1 A_SetPitch(pitch-0.2,SPF_INTERPOLATE)
		SSHR FG 1
		{
		 A_SetPitch(pitch+0.2,SPF_INTERPOLATE);
		 A_WeaponReady(WRF_NOBOB);
		}
		TNT1 A 0 
		{
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		SSHR A 0 A_Refire
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Goto DTShellChecker
			
	DTChamberInsertShell:
        TNT1 A 0
		TNT1 A 0 A_Takeinventory("PBPumpShotgunWasEmpty",1)
		SSHR A 1 A_WeaponReady(WRF_NOBOB)
		SSHR H 2 
		TNT1 A 0 A_PlaySound("weapons/sgpump",1)
		SSHR I 3
		SSHR J 1 A_PlaySound("insertshell",3)
		SSHR KLMNOPP 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTS");}
		 If(CountInv("HasDragonBreath") >=1 )
		   {A_SetWeaponSprite("SHTD");}
		 return state("");
		}
		TNT1 A 0 
		{
		 A_PlaySound("weapons/sgmvpump",2);
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		Goto DTShellChecker
	
	DTReloadFinished:
	    SHTM A 1
	    SHTM BCDEFG 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		 {A_SetWeaponSprite("SHMS");} 
		 If(CountInv("HasDragonBreath") >=1 )
		 {A_SetWeaponSprite("SHMD");}
		 return state("");
		}
		SHTM H 1 
		TNT1 A 0 A_PlaySound("RIFCL_IN", 3)
		SHTM I 1 
		TNT1 A 0 A_PlaySound("insertshell", 4)
        SHTM JKLM 1 
	DTShellCheckerMag:	
		TNT1 A 0 A_JumpIfInventory("NewShell",1,1)
        Goto DTReloadMagFinished
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",19,"ReloadMagFinished")
		TNT1 A 0 
		{
		 A_Giveinventory("ShotgunAmmo",1);
		 A_Takeinventory("NewShell",1);
		}
		Goto DTShellCheckerMag
	DTReloadMagFinished:
	    SHMG IHG 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		SHTG FEDCB 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		TNT1 A 0
		{
		 A_Takeinventory("Reloading",1);
		 A_Takeinventory("PB_LockScreenTilt",1);
		 A_SetRoll(0,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_Refire
		Goto Ready3
	//////////////////////////////////////
		
	OkToFire:
		SHTG A 1 A_WeaponReady
		Goto Ready3
			
	Unload:
		SHTG A 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_GiveInventory("PBPumpShotgunWasEmpty", 1)
        TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,3)
        Goto OkToFire
        TNT1 AAA 0
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		SHTG BCDEFGHIJK 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE)
		
	RemoveBullets:
		TNT1 AAAA 0
		TNT1 A 0 A_JumpIfInventory("ShotgunAmmo",1,3)
		Goto FinishUnload
        TNT1 AAAAAA 0
		TNT1 A 0 A_PlaySound("weapons/sgmvpump")
	ActuallyUnload:
		SHTG L 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTA");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHTF");}
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		 return state("");
		}
		SHTG M 1
		{
		 A_SetRoll(roll-0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch+0.1,SPF_INTERPOLATE);
		}
		SHTG N 1
		SHTG M 1 
		{
		 A_SetRoll(roll+0.4,SPF_INTERPOLATE);
		 A_SetPitch(pitch-0.1,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_JumpIfInventory("NewShell", 1,3) 
		SSHR H 1 A_SetRoll(roll-0.1,SPF_INTERPOLATE) // So the chamber shows as being empty
		TNT1 A 0 
		{
		 return state (2); // Skip This Frame if the shotgun isn't loaded.
		} 
		SHTG L 1
		{
		 If(CountInv("HasSlugs") >=1 )        
		   {A_SetWeaponSprite("SHTA");} 
		 If(CountInv("HasDragonBreath") >=1 )        
		   {A_SetWeaponSprite("SHTF");}
		 A_SetRoll(roll-0.1,SPF_INTERPOLATE);
		 return state("");
		}
        TNT1 A 0 A_PlaySound("weapons/sgmvpump")
		TNT1 A 0 A_Takeinventory("ShotgunAmmo",1)
		TNT1 A 0 A_Giveinventory("NewShell",1)
		SHTG K 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		Goto RemoveBullets
		
	FInishUnload:
		SHTG KJIHGFED 1 A_SetRoll(roll+0.1,SPF_INTERPOLATE)
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		TNT1 A 0 A_Takeinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto OkToFire	

	Spawn:
		SHTC A -1
		Stop
	
    FlashKicking:
	    TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine", 1, "MagFlashKick")
	    SHTG CDFGH 1
		SHTG I 4
		SHTG HGEDB 1
		Stop
	
	MagFlashKick:
	    SHTG CDF 1
		SHMG GH 1
		SHMG I 4
		SHMG HG 1
		SHTG EDB 1
		Stop
		
	FlashAirKicking:
	    TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine", 1, "MagFlashAirKick")
	    SHTG CDFGH 1
		SHTG I 6
		SHTG HGEDB 1
		Stop
	
	MagFlashAirKick:
	    SHTG CDF 1
		SHMG GH 1
		SHMG I 6
		SHMG HG 1
		SHTG EDB 1
		Stop
		
	FlashSlideKicking:
	    TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine", 1, "MagFlashSlideKicking")
		SHTG CDFGH 1
		SHTG I 16
		SHTG HGEDB 1
		Stop
	
	MagFlashSlideKick:
	    SHTG CDF 1
		SHMG GH 1
		SHMG I 16
		SHMG HG 1
		SHTG EDB 1
		Stop

	FlashSlideKickingStop:
	    TNT1 A 0 A_JumpIfInventory("PumpshotgunMagazine", 1, "MagFlashSlideKickingStop")
		SHTG HGEDB 1
		Stop
	
	MagFlashSlideKickingStop:
	    SHMG HG 1
		SHTG EDB 1
		Stop
		
	FlashPunching:
        Goto FlashKicking
	}
}

ACTOR ShotgunAmmo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 9
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 9
   Inventory.Icon "SHTCA1"
}